{:min-bb-version "0.9.0"

 :tasks
 {:requires ([babashka.process :refer [shell]]
             [babashka.http-client :as http]
             [cheshire.core :as json]
             [clojure.string :as str])

  :init
  (do
    (def aws-region "eu-west-1")
    (def project-name "autoscaling-example")

    (defn git-tag []
      (str/trim (:out (shell {:out :string} "git describe --tags --always"))))

    (defn aws-account []
      (str/trim (:out (shell {:out :string}
                             "aws sts get-caller-identity --query Account --output text"))))

    (defn registry []
      (str (aws-account) ".dkr.ecr." aws-region ".amazonaws.com"))

    (defn alb-dns []
      (str/trim (:out (shell {:out :string :dir "terraform"}
                             "terraform output -raw alb_dns_name"))))

    (def http1-client (http/client {:version :http1.1})))

  build
  {:doc "ECR login + multi-platform buildx of web & worker, pushed to ECR"
   :task
   (let [reg (registry)
         tag (git-tag)
         pw  (str/trim (:out (shell {:out :string}
                                    "aws ecr get-login-password --region" aws-region)))]
     (shell {:in pw} "docker login --username AWS --password-stdin" reg)
     (try
       (shell "docker buildx create --name multiarch --use")
       (catch Exception _
         (shell "docker buildx use multiarch")))
     (println "Building web:" (str reg "/" project-name "/web:" tag))
     (shell "docker buildx build"
            "--platform" "linux/amd64,linux/arm64"
            "--build-arg" "BUILD_TARGET=uber-web"
            "-t" (str reg "/" project-name "/web:" tag)
            "--push" ".")
     (println "Building worker:" (str reg "/" project-name "/worker:" tag))
     (shell "docker buildx build"
            "--platform" "linux/amd64,linux/arm64"
            "--build-arg" "BUILD_TARGET=uber-worker"
            "-t" (str reg "/" project-name "/worker:" tag)
            "--push" "."))}

  deploy
  {:doc "Terraform apply with current git tag"
   :task
   (let [tag (git-tag)]
     (println "Deploying image tag:" tag)
     (apply shell
            {:dir "terraform"}
            "terraform apply"
            (str "-var=image_tag=" tag)
            *command-line-args*))}

  add-jobs
  {:doc "POST jobs to the ALB. Args: [n] (default 1)"
   :task
   (let [dns (alb-dns)
         n   (parse-long (or (first *command-line-args*) "1"))
         url (str "http://" dns "/jobs")]
     (doseq [i (range n)]
       (let [body (json/generate-string {:type "example"
                                         :payload {:data (str "job-" (inc i))}})
             resp (http/post url {:client http1-client
                                  :headers {"Content-Type" "application/json"}
                                  :body body})]
         (println (:body resp))))
     (when (> n 1)
       (println "Added" n "jobs")))}

  queue-length
  {:doc "GET /queue-length from the ALB"
   :task
   (let [dns  (alb-dns)
         resp (http/get (str "http://" dns "/queue-length")
                        {:client http1-client})]
     (println (:body resp)))}

  logs-web
  {:doc "Tail web service logs"
   :task
   (shell "aws logs tail" (str "/ecs/" project-name "/web")
          "--follow" "--region" aws-region)}

  logs-worker
  {:doc "Tail worker service logs"
   :task
   (shell "aws logs tail" (str "/ecs/" project-name "/worker")
          "--follow" "--region" aws-region)}}}
